plugins {
    id "org.sonarqube" version "2.8"
}

apply plugin: 'com.android.application'
apply plugin: "com.hiya.jacoco-android"

repositories {
    maven { url "https://mint.splunk.com/gradle/" }
    maven { url "https://jitpack.io" }
}

def version_code = 89
def version_name = "7.3.0"

android {
    compileSdkVersion 30

    defaultConfig {
        versionCode version_code
        versionName version_name

        applicationId "org.digitalcampus.mobile.learning"

        minSdkVersion 21
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'README.md'
    }

    lintOptions {
        checkReleaseBuilds true
        // Disable checks for String translations
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    // Configure to use Java8 features like try-with-resources
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    Properties runtimeProps = new Properties()
    runtimeProps.load(new FileInputStream(rootProject.file('oppia-default.properties')))

    def oppiaPropsFile = rootProject.file('custom.properties')
    if (oppiaPropsFile.canRead()) {
        logger.lifecycle('Fetching properties from external file')
        runtimeProps.load(new FileInputStream(oppiaPropsFile))
    } else {
        logger.error('No properties file found. Using default values.')
    }

    buildTypes {

        all {
            //Define all the constant values that depend on the config properties

            def analyticsLibrary = "none"
            if (runtimeProps.containsKey('ANALYTICS_LIBRARY')){
                analyticsLibrary = runtimeProps['ANALYTICS_LIBRARY']
                if (analyticsLibrary == ""){ analyticsLibrary = "none" }
            }
            if (analyticsLibrary == "none"){
                logger.error('No analytics library configured, will use empty value')
            }

            buildConfigField "String", "ANALYTICS_LIBRARY", "\"" + analyticsLibrary + "\""
            buildConfigField "String", "COUNTLY_APP_KEY", "\"" + runtimeProps.getProperty('COUNTLY_APP_KEY','') + "\""
            buildConfigField "String", "MINT_API_KEY", "\"" + runtimeProps.getProperty('MINT_API_KEY','') + "\""
            buildConfigField "String", "COUNTLY_SERVER_URL", "\"" + runtimeProps.getProperty('COUNTLY_SERVER_URL','') + "\""

            buildConfigField "boolean", "ADMIN_PROTECT_SETTINGS", runtimeProps['ADMIN_PROTECT_SETTINGS']
            buildConfigField "boolean", "ADMIN_PROTECT_ACTIVITY_SYNC", runtimeProps['ADMIN_PROTECT_ACTIVITY_SYNC']
            buildConfigField "boolean", "ADMIN_PROTECT_ACTIVITY_EXPORT", runtimeProps['ADMIN_PROTECT_ACTIVITY_EXPORT']
            buildConfigField "boolean", "ADMIN_PROTECT_COURSE_DELETE", runtimeProps['ADMIN_PROTECT_COURSE_DELETE']
            buildConfigField "boolean", "ADMIN_PROTECT_COURSE_RESET", runtimeProps['ADMIN_PROTECT_COURSE_RESET']
            buildConfigField "boolean", "ADMIN_PROTECT_COURSE_INSTALL", runtimeProps['ADMIN_PROTECT_COURSE_INSTALL']
            buildConfigField "boolean", "ADMIN_PROTECT_COURSE_UPDATE", runtimeProps['ADMIN_PROTECT_COURSE_UPDATE']

            buildConfigField "boolean", "MENU_ALLOW_LOGOUT", runtimeProps['MENU_ALLOW_LOGOUT']
            buildConfigField "boolean", "MENU_ALLOW_MONITOR", runtimeProps['MENU_ALLOW_MONITOR']
            buildConfigField "boolean", "MENU_ALLOW_SETTINGS", runtimeProps['MENU_ALLOW_SETTINGS']
            buildConfigField "boolean", "MENU_ALLOW_LANGUAGE", runtimeProps['MENU_ALLOW_LANGUAGE']
            buildConfigField "boolean", "MENU_ALLOW_SYNC", runtimeProps['MENU_ALLOW_SYNC']
            buildConfigField "boolean", "MENU_ALLOW_COURSE_DOWNLOAD", runtimeProps['MENU_ALLOW_COURSE_DOWNLOAD']
            buildConfigField "boolean", "SESSION_EXPIRATION_ENABLED", runtimeProps['SESSION_EXPIRATION_ENABLED']
            buildConfigField "boolean", "OFFLINE_REGISTER_ENABLED", runtimeProps['OFFLINE_REGISTER_ENABLED']
            buildConfigField "int", "SESSION_EXPIRATION_TIMEOUT", runtimeProps['SESSION_EXPIRATION_TIMEOUT']
            buildConfigField "int", "DOWNLOAD_COURSES_DISPLAY", runtimeProps['DOWNLOAD_COURSES_DISPLAY']
            buildConfigField "boolean", "SHOW_COURSE_DESCRIPTION", runtimeProps['SHOW_COURSE_DESCRIPTION']

            buildConfigField "boolean", "METADATA_INCLUDE_NETWORK", runtimeProps['METADATA_INCLUDE_NETWORK']
            buildConfigField "boolean", "METADATA_INCLUDE_APP_INSTANCE_ID", runtimeProps['METADATA_INCLUDE_APP_INSTANCE_ID']
            buildConfigField "boolean", "METADATA_INCLUDE_MANUFACTURER_MODEL", runtimeProps['METADATA_INCLUDE_MANUFACTURER_MODEL']
            buildConfigField "boolean", "METADATA_INCLUDE_WIFI_ON", runtimeProps['METADATA_INCLUDE_WIFI_ON']
            buildConfigField "boolean", "METADATA_INCLUDE_NETWORK_CONNECTED", runtimeProps['METADATA_INCLUDE_NETWORK_CONNECTED']
            buildConfigField "boolean", "METADATA_INCLUDE_BATTERY_LEVEL", runtimeProps['METADATA_INCLUDE_BATTERY_LEVEL']

            buildConfigField "String", "GAMIFICATION_MEDIA_CRITERIA", "\"" + runtimeProps['GAMIFICATION_MEDIA_CRITERIA'] + "\""
            buildConfigField "int", "GAMIFICATION_DEFAULT_MEDIA_THRESHOLD", runtimeProps['GAMIFICATION_DEFAULT_MEDIA_THRESHOLD']
            buildConfigField "String", "GAMIFICATION_POINTS_ANIMATION", "\"" + runtimeProps['GAMIFICATION_POINTS_ANIMATION'] + "\""
            buildConfigField "int", "DURATION_GAMIFICATION_POINTS_VIEW", runtimeProps['DURATION_GAMIFICATION_POINTS_VIEW']
            buildConfigField "int", "ADMIN_PASSWORD_OVERRIDE_VERSION", runtimeProps['ADMIN_PASSWORD_OVERRIDE_VERSION']
            buildConfigField "String", "ADMIN_PROTECT_INITIAL_PASSWORD", "\"" + runtimeProps['ADMIN_PROTECT_INITIAL_PASSWORD'] + "\""

            buildConfigField "int", "LOAD_CUSTOMFIELDS_VERSION", runtimeProps['LOAD_CUSTOMFIELDS_VERSION']

            buildConfigField "boolean", "START_COURSEINDEX_COLLAPSED", runtimeProps['START_COURSEINDEX_COLLAPSED']
            resValue "string", "MENU_ALLOW_LOGOUT_DEFAULT_VALUE", runtimeProps['MENU_ALLOW_LOGOUT']
            resValue "string", "MENU_ALLOW_COURSE_DOWNLOAD_DEFAULT_VALUE", runtimeProps['MENU_ALLOW_COURSE_DOWNLOAD']
            resValue "string", "MENU_ALLOW_LANGUAGE_DEFAULT_VALUE", runtimeProps['MENU_ALLOW_LANGUAGE']
            resValue "string", "START_COURSEINDEX_COLLAPSED", runtimeProps['START_COURSEINDEX_COLLAPSED']
            resValue "string", "prefServerDefault", runtimeProps['OPPIA_SERVER_DEFAULT']
            resValue "string", "oppiaServerHost", runtimeProps['OPPIA_SERVER_HOST']
            resValue "string", "oppiaServerDomain", runtimeProps['OPPIA_SERVER_DOMAIN']
            resValue "string", "prefAdminPasswordDefault", runtimeProps['ADMIN_PROTECT_INITIAL_PASSWORD']
            resValue "string", "prefGamificationPointsAnimationDefault", runtimeProps['GAMIFICATION_POINTS_ANIMATION']
            resValue "string", "prefDurationGamificationPointsViewDefault", runtimeProps['DURATION_GAMIFICATION_POINTS_VIEW']
            resValue "string", "SHOW_COURSE_DESCRIPTION", runtimeProps['SHOW_COURSE_DESCRIPTION']

            resValue "string", "version_name", version_name
        }

        debug {
            multiDexEnabled true
            testCoverageEnabled = true
            debuggable = true
        }

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        animationsDisabled = true

        unitTests.all {
            jacoco {
                jacoco.includeNoLocationClasses = true
                jacoco.excludes = ['jdk.internal.*']
            }
        }
    }

    flavorDimensions "main"

    productFlavors {
        normal { dimension "main" }
    }

    useLibrary 'android.test.mock'

    android.buildFeatures.viewBinding = true
}

// Setup Jacoco version. It's recommended way to exclude conflicts between components
jacoco {
    toolVersion = "0.8.3"
}


dependencies {
    def workVersion = '2.5.0'
    def roomVersion = "1.1.1"
    def fragmentVersion = '1.2.5'
    def daggerVersion = '2.27'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'joda-time:joda-time:2.3'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.4'
    implementation 'androidx.navigation:navigation-ui:2.3.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "androidx.work:work-runtime:$workVersion"

    androidTestImplementation "androidx.work:work-testing:$workVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    implementation 'javax.xml.stream:stax-api:1.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.okhttp3:okhttp:4.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'
    implementation 'com.splunk.mint:mint:5.0.0'
    implementation 'ly.count.android:sdk:20.11.8'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.mikhaellopez:circularprogressbar:3.1.0'
    implementation 'com.github.badoualy:stepper-indicator:1.0.7'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.hbb20:ccp:2.5.1' //Phone code picker

    //Dagger Dependencies
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.5'

    implementation "com.squareup.inject:assisted-inject-annotations-dagger2:0.3.3"

    //JUnit Dependencies
    testImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.4.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.3.0'

    //Espresso Dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
    }

    // Fragment testing
    implementation "androidx.fragment:fragment:$fragmentVersion"
    debugImplementation("androidx.fragment:fragment-testing:$fragmentVersion") {
        // exclude androidx.test:core while fragment_testing depends on 1.1.0
        exclude group: 'androidx.test', module: 'core'
    }

    //Mockito Dependencies
    testImplementation 'org.mockito:mockito-core:2.4.0'

    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'


//    debugImplementation 'im.dino:dbinspector:3.4.1@aar'

}


task grantPermissions(type: Exec, dependsOn: 'installNormalDebug') {
    group = 'test'
    description = 'Grant permissions for testing.'

    def adb = android.getAdbExe().toString()
    if (!System.getProperty('os.name').toLowerCase().contains('windows')) {
        logger.lifecycle('Granting permissions for a non windows os')
    } else {
        logger.lifecycle('Granting permissions for a windows os')
    }

    commandLine "${adb} shell pm grant ${android.defaultConfig.applicationId} android.permission.SET_ANIMATION_SCALE".split(' ')
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connectedAndroidTest') ||
            task.name.startsWith('connectedDebugAndroidTest') ||
            task.name.startsWith('assembleDebugAndroidTest') ||
            task.name.startsWith('assembleAndroidTest')) {
        task.dependsOn grantPermissions
    }
}

sonarqube {
    properties {
        property "sonar.projectVersion", android.defaultConfig.versionName
        property "sonar.projectName", "Oppia Core app"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sources", "src/main/java"
        property "sonar.binaries", "build/intermediates/app_classes/normalDebug"
        property "sonar.java.binaries", "build/intermediates/app_classes/normalDebug"
        property "sonar.tests", "src/test/java, src/androidTest/java"
        property "sonar.java.test.binaries", "build/intermediates/app_classes/normalDebug"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/coverage/normal/debug/report.xml"
        property "sonar.exclusions", "app/src/test/java/**/*, app/src/androidTest/java/**/*, " +
                "src/test/java/**/*, src/androidTest/**/*, src/test/java, src/androidTest/java"
        property "sonar.test.exclusions", "app/src/test/java/**/*, app/src/androidTest/java/**/*, " +
                "src/test/java/**/*, src/androidTest/**/*, src/test/java, src/androidTest/java"
        property "sonar.coverage.exclusions", "app/src/test/java/**/*, app/src/androidTest/java/**/*, " +
                "src/test/java/**/*, src/androidTest/**/*, src/test/java, src/androidTest/java"

        def sonarqubePropsFile = rootProject.file('sonarqube.properties')
        if (sonarqubePropsFile.canRead()) {
            def sonarqubeProps = new Properties()
            sonarqubeProps.load(new FileInputStream(sonarqubePropsFile))

            property "sonar.projectKey", sonarqubeProps["sonar.projectKey"]
            property "sonar.organization", sonarqubeProps["sonar.organization"]
            property "sonar.login", sonarqubeProps["sonar.login"]
        }
    }
}
tasks.sonarqube.dependsOn connectedCheck
tasks.sonarqube.dependsOn jacocoTestReport
